# Maintainer: Dominik Opyd <d.opyd@oad.earth>
# Contributor: thisischrys <thisischrys+aur@gmail.com>
# Contributor: Nitroretro <nitroretro@protonmail.com>

# Based on the `minecraft-server` AUR package by:
## Maintainer: Gordian Edenhofer <gordian.edenhofer@gmail.com>
## Contributor: Philip Abernethy <chais.z3r0@gmail.com>
## Contributor: sowieso <sowieso@dukun.de>

minver=1.20
minpath=4
srvpath=/srv/forge

pkgver=49.0.27
pkgrel=2
pkgname=forge-server
pkgdesc="Forge is a free, open-source modding API all of your favourite mods use!"
url="https://minecraftforge.net"
arch=('any')
license=("custom")
depends=("jdk-openjdk" "tmux")
makedepends=("jdk-openjdk")
optdepends=("tar: needed in order to create world backups"
						"netcat: required in order to suspend an idle server")
provides=("forge-server=${pkgver}")
backup=("etc/conf.d/forge")
install=forge-server.install
source=(git+https://github.com/Edenhofer/minecraft-server
				https://raw.githubusercontent.com/MinecraftForge/MinecraftForge/${minver}.x/LICENSE.txt
				https://maven.minecraftforge.net/net/minecraftforge/forge/${minver}.${minpath}-${pkgver}/forge-${minver}.${minpath}-${pkgver}-installer.jar)
noextract=(forge-${minver}.${minpath}-${pkgver}-installer.jar)
b2sums=('SKIP'
            'SKIP'
            'SKIP')

frgpath=${srvpath}/libraries/net/minecraftforge/forge/${minver}.${minpath}-${pkgver}

prepare() {
	mkdir -p $pkgname && cd $pkgname

	java -jar ../forge-${minver}.${minpath}-${pkgver}-installer.jar --installServer
}

build() {
	cd minecraft-server

	make \
		GAME=forge \
		INAME=forged \
		SERVER_ROOT=${srvpath} \
		SERVER_START_CMD="java @${frgpath}/user_jvm_args.txt @${frgpath}/unix_args.txt" \
		MAIN_EXECUTABLE=forge-${minver}.${minpath}-${pkgver}-server.jar \
	all
}

package() {
	cd minecraft-server

	make \
		GAME=forge \
		INAME=forged \
		DESTDIR=$pkgdir \
	install
	
	cd ../forge-server

	rm *.log *.bat

	rm libraries/net/minecraftforge/forge/${minver}.${minpath}-${pkgver}/win_args.txt

	find libraries -type f -print0 | xargs -0 -i@ install -Dm644 "@" "${pkgdir}${srvpath}/@"

	install -Dm 644 user_jvm_args.txt ${pkgdir}${frgpath}/user_jvm_args.txt
	install -Dm 644 $srcdir/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

	mkdir -p $pkgdir/var/log/

	install -dm2755 $pkgdir$srvpath/logs
	install -dm2755 $pkgdir$srvpath/mods

	ln -s ${srvpath}/logs $pkgdir/var/log/forge

	chmod g+ws $pkgdir$srvpath
}
